import edu.wpi.first.gradlerio.GradleRIOPlugin

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "edu.wpi.first.GradleRIO" version "2019.2.1"
}

def ROBOT_MAIN_CLASS = "com.amhsrobotics.tko2019.Robot"

group 'com.amhsrobotics'
version '1.0.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    //noinspection GroovyAssignabilityCheck
    compile wpi.deps.wpilib()
    //noinspection GroovyAssignabilityCheck
    compile wpi.deps.vendor.java()
    //noinspection GroovyAssignabilityCheck
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    //noinspection GroovyAssignabilityCheck
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

deploy {
    targets {
        roboRIO("roborio") {
            team = 1351
        }
    }
    artifacts {
        frcJavaArtifact('frcJava') {
            //noinspection GroovyAssignabilityCheck
            targets << "roborio"
        }
        fileTreeArtifact('frcStaticFileDeploy') {
            files = fileTree(dir: 'src/main/resources')
            //noinspection GroovyAssignabilityCheck
            targets << "roborio"
            directory = '/home/lvuser/deploy'
        }
    }
}

jar {
    from {
        configurations.compile.collect {
            if (it.isDirectory()) {
                it
            } else {
                zipTree(it)
            }
        }
    }
    //noinspection GroovyAssignabilityCheck
    manifest GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.11"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.11"
    }
}
